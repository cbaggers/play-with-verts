(in-package #:play-with-verts)

;;------------------------------------------------------------
;; Textures & Samplers
;;
;; We cache the data based on the the path so we don't
;; get lots of instances in memory

(defvar *samplers* (make-hash-table :test #'equal))

(defun get-tex (path &optional (force nil) (mipmap t))
  (when force
    (let ((s (gethash path *samplers*)))
      (when s
        (free (sampler-texture s)))
      (remhash path *samplers*)))
  (or (gethash path *samplers*)
      (setf (gethash path *samplers*)
            (sample
             (dirt:load-image-to-texture
              (asdf:system-relative-pathname
               :play-with-verts path)
              :rgba8
              mipmap
              t)))))

(defun get-cube-tex (paths &optional (force nil) (mipmap t))
  (assert (= (length paths) 6))
  (let ((key (format nil "狺疳翳螬┅麒孱骘蜚戾èㄧ弭栳箬脲筢眇戾蝮┅麒孱ㄦ蝈筢眇戾颦翦趱蝈螬┅蝈龛狍脲筢眇戾蝮┅矧ㄧ弭栳箬脲筢眇戾蝮鏖翳悱狎蜥蟓骝邋ㄣ磲疸狎灬礅溽皓ㄤ轵艉祜徜轫徵瀛麸悱狎蜥ㄡ箐婧簌篝屙蝈灬糸鲥疳翳钺礤吼灬鏖翳鲥螋皓候玮岍疳翳螬戾è筢眇戾磲脲翦趱蝈汜哄戾礤铘豉疱候玮岣恒踱弩喉轲磲黹痦狃虹孱弪狒瀛黹痦狃舂┅箦翩黩狃螬恒灬眇麸邃珏箦翩ㄧ弭栳箬脲筢眇戾蝮螬┅┅换